[===================================================================================================
Author: Corey Bottorff
File Name: CMB.RELATIONSHIP.MM.RATE.REVIEW
Date Created: 11/22/2017
Description: Reviews all of the Relationship Money Market accounts to see if they qualified for
             the higher dividend rate.
Revision History:
DATE:        AUTHOR:                      DESCRIPTION:
----------   --------------------------   ---------------------------------------------------------
===================================================================================================]

TARGET = SHARE

DEFINE
 #INCLUDE "RB.LISTEXPAND.DEF"
 #INCLUDE "RD.OUTPUT.DEF"
 
 FMNUM = NUMBER
 EXCEPTIONRPTNUM = NUMBER
 ERRORTEXT = CHARACTER
 RELATIONSHIPMMSHARETYPE = NUMBER
 NORMALMMSHARETYPE = NUMBER
 ABSCHKSHARETYPE = NUMBER
 MINIMUMDDAMOUNT = MONEY
 MINIMUMPURCHASES = NUMBER
 STARTDATE = DATE
 ENDDATE = DATE
 MEMBERNAME = CHARACTER
 HASABSOLUTECHECKING = NUMBER
 ABSCHKDIRECTDEPOSITAMOUNT = MONEY
 PURCHASECOUNT = NUMBER
 NEWDIVTYPE = NUMBER
 RELATIONSHIPRATECOUNT = NUMBER
 NORMALRATECOUNT = NUMBER
 
 TRUE = 1
 FALSE = 0
END [END DEFINE]

SETUP
 CALL GETUSERINPUT

 OUTPUTOPEN(OUTPUTDEVREPORT,0,"Relationship MM Rate - FM","FMREPORTS",FMNUM,ERRORTEXT)
 OUTPUTOPEN(OUTPUTDEVREPORT,0,"Relationship MM Rate - Ineligible Report","SHAREREPOR",EXCEPTIONRPTNUM,ERRORTEXT)
END [END SETUP]

SELECT
 SHARE:TYPE = RELATIONSHIPMMSHARETYPE AND
 SHARE:CLOSEDATE = '--/--/--' AND
 SHARE:CHARGEOFFDATE = '--/--/--'
END [END SELECT]

PRINT TITLE = "Relationship MM Rate - Eligible Report" REPORTCATEGORY="SHAREREPOR"
 CALL GETMEMBERNAME
 CALL CHECKFORABSOLUTECHECKING
 CALL GETABSCHKDIRECTDEPOSITAMOUNT
 CALL COUNTQUALIFYINGPURCHASES
 
 IF HASABSOLUTECHECKING = TRUE AND
    (ABSCHKDIRECTDEPOSITAMOUNT >= MINIMUMDDAMOUNT OR
     PURCHASECOUNT >= MINIMUMPURCHASES) THEN
   CALL ASSIGNRELATIONSHIPRATE
 ELSE
   CALL ASSIGNLOWERRATE
END [END PRINT]

TOTAL
 CALL PRINTINCREASEDTOTAL
 CALL PRINTNOCHANGETOTAL
END [END TOTAL]

PROCEDURE GETMEMBERNAME
 MEMBERNAME = ""
 
 FOR ACCOUNT ACCOUNT:NUMBER
  DO
   FOR EACH NAME WITH (NAME:TYPE = 0)
    DO
     MEMBERNAME = NAME:LONGNAME
    END [END FOR EACH NAME]
  END [END FOR ACCOUNT]
END [END GETMEMBERNAME]

PROCEDURE CHECKFORABSOLUTECHECKING
 HASABSOLUTECHECKING = FALSE
 
 FOR ACCOUNT ACCOUNT:NUMBER
  DO
   FOR EACH SHARE WITH (SHARE:CLOSEDATE = '--/--/--' AND
                        SHARE:CHARGEOFFDATE = '--/--/--' AND
                        SHARE:TYPE = ABSCHKSHARETYPE)
    DO
     HASABSOLUTECHECKING = TRUE
    END UNTIL HASABSOLUTECHECKING = TRUE [END FOR EACH SHARE]
  END [END FOR ACCOUNT]
END [END CHECKFORABSOLUTECHECKING]

PROCEDURE GETABSCHKDIRECTDEPOSITAMOUNT
 ABSCHKDIRECTDEPOSITAMOUNT = $0.00
 
 FOR ACCOUNT ACCOUNT:NUMBER
  DO
   FOR EACH SHARE WITH ((SHARE:CLOSEDATE = '--/--/--' OR
                         SHARE:CLOSEDATE >= STARTDATE) AND
                        SHARE:CHARGEOFFDATE = '--/--/--' AND
                        SHARE:TYPE = ABSCHKSHARETYPE)
    DO
     FOR EACH SHARE TRANSACTION WITH (SHARE TRANSACTION:VOIDCODE = FALSE AND
                                      SHARE TRANSACTION:COMMENTCODE = FALSE AND
                                      SHARE TRANSACTION:POSTDATE >= STARTDATE AND
                                      SHARE TRANSACTION:POSTDATE <= ENDDATE AND
                                      SHARE TRANSACTION:ACTIONCODE = "D" AND
                                      (SHARE TRANSACTION:SOURCECODE = "E" OR
                                       SHARE TRANSACTION:SOURCECODE = "P"))
      DO
       ABSCHKDIRECTDEPOSITAMOUNT = ABSCHKDIRECTDEPOSITAMOUNT + SHARE TRANSACTION:TRANAMOUNT
      END UNTIL SHARE TRANSACTION:POSTDATE < STARTDATE [END FOR EACH SHARE TRANSACTION]
    END [END FOR EACH SHARE]
  END [END FOR ACCOUNT]
END [END GETABSCHKDIRECTDEPOSITAMOUNT]

PROCEDURE COUNTQUALIFYINGPURCHASES
 PURCHASECOUNT = 0

 FOR ACCOUNT ACCOUNT:NUMBER
  DO
   FOR EACH SHARE WITH ((SHARE:CLOSEDATE = '--/--/--' OR
                         SHARE:CLOSEDATE >= STARTDATE) AND
                        SHARE:CHARGEOFFDATE = '--/--/--')
    DO
     FOR EACH SHARE TRANSACTION WITH (SHARE TRANSACTION:VOIDCODE = FALSE AND
                                      SHARE TRANSACTION:COMMENTCODE = FALSE AND
                                      SHARE TRANSACTION:POSTDATE >= STARTDATE AND
                                      SHARE TRANSACTION:POSTDATE <= ENDDATE AND
                                      (SHARE TRANSACTION:SOURCECODE = "G" OR
                                       SHARE TRANSACTION:SOURCECODE = "O"))
      DO
       PURCHASECOUNT = PURCHASECOUNT + 1
      END UNTIL SHARE TRANSACTION:POSTDATE < STARTDATE [END FOR EACH SHARE TRANSACTION]
    END [END FOR EACH SHARE]
  END [END FOR ACCOUNT]
END [END COUNTQUALIFYINGPURCHASES]

PROCEDURE ASSIGNRELATIONSHIPRATE
 RELATIONSHIPRATECOUNT = RELATIONSHIPRATECOUNT + 1
   
 IF SHARE:DIVTYPE <> RELATIONSHIPMMSHARETYPE THEN
  DO
   NEWDIVTYPE = RELATIONSHIPMMSHARETYPE
   CALL CHANGEDIVTYPE
  END [END IF]
   
 CALL WRITETOREPORT
END [END ASSIGNRELATIONSHIPRATE]

PROCEDURE ASSIGNLOWERRATE
 NORMALRATECOUNT = NORMALRATECOUNT + 1
  
 IF SHARE:DIVTYPE <> NORMALMMSHARETYPE THEN
  DO
   NEWDIVTYPE = NORMALMMSHARETYPE
   CALL CHANGEDIVTYPE
  END [END IF]
     
 CALL WRITETOEXCEPTIONREPORT
END [END ASSIGNLOWERRATE]

PROCEDURE CHANGEDIVTYPE
 OUTPUTSWITCH(FMNUM,ERRORTEXT)
 
 PRINT "ACCOUNT " + ACCOUNT:NUMBER + " MODIFY SHARE " + SHARE:ID
 NEWLINE
 PRINT " CHANGE DIVTYPE FROM "
 COL = 049 LEFT SHARE:DIVTYPE
 COL = 089 " TO "
 COL = 093 LEFT NEWDIVTYPE
 NEWLINE
 
 OUTPUTSWITCH(OUTPUTCHANNELDEFAULT,ERRORTEXT)
END [END CHANGEDIVTYPE]

PROCEDURE WRITETOEXCEPTIONREPORT
 OUTPUTSWITCH(EXCEPTIONRPTNUM,ERRORTEXT)

 CALL WRITETOREPORT
 
 OUTPUTSWITCH(OUTPUTCHANNELDEFAULT,ERRORTEXT)
END [END WRITETOEXCEPTIONREPORT]

PROCEDURE WRITETOREPORT
 HEADER = REPEATCHR("=",132)
          [123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012]
 HEADER = "Account#     Member Name                             Open Abs Chk?   Abs Chk Direct Deposits   Qualifying Purchases"
 HEADER = REPEATCHR("=",132)
 
 COL = 001 ACCOUNT:NUMBER
 COL = 014 MEMBERNAME
 
 IF HASABSOLUTECHECKING = TRUE THEN
   COL = 060 "Y"
 ELSE
   COL = 060 "N"
 
 COL = 076 LEFT ABSCHKDIRECTDEPOSITAMOUNT
 COL = 105 LEFT PURCHASECOUNT
 NEWLINE
END [END WRITETOREPORT]

PROCEDURE PRINTINCREASEDTOTAL
 NEWLINE
 NEWLINE
 PRINT "Total Shares: "
 PRINT RELATIONSHIPRATECOUNT
END [END PRINTINCREASEDTOTAL]

PROCEDURE PRINTNOCHANGETOTAL
 OUTPUTSWITCH(EXCEPTIONRPTNUM,ERRORTEXT)

 NEWLINE
 NEWLINE
 PRINT "Total Shares: "
 PRINT NORMALRATECOUNT
 
 OUTPUTSWITCH(OUTPUTCHANNELDEFAULT,ERRORTEXT)
END [END PRINTNOCHANGETOTAL]

PROCEDURE GETUSERINPUT
 RELATIONSHIPMMSHARETYPE = CODEREAD("Relationship MM Share Type")
 NORMALMMSHARETYPE = CODEREAD("Normal MM Share Type")
 ABSCHKSHARETYPE = CODEREAD("Absolute Checking Share Type") 
 MINIMUMDDAMOUNT = MONEYREAD("Minimum Abs Chk Direct Deposit Amt")
 MINIMUMPURCHASES = NUMBERREAD("Minimum Qualifying Purchases")
 STARTDATE = DATEREAD("Start Date")
 
 IF STARTDATE = '--/--/--' THEN
   STARTDATE = DATEOFFSET(SYSTEMDATE,0,1)
   
 ENDDATE = DATEREAD("End Date")
 
 IF ENDDATE = '--/--/--' THEN
   ENDDATE = DATEOFFSET(SYSTEMDATE,0,31)
END [END GETUSERINPUT]

#INCLUDE "RB.LISTEXPAND"
